import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta7'
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group = 'com.nextplugins'
version = '2.2.1'

repositories {
    mavenCentral()

    // spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    // libs
    maven { url = 'https://jitpack.io/' }
    maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://repo.nickuc.com/maven-releases/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'

    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.11.6'
    compileOnly 'me.filoghost.holographicdisplays:holographicdisplays-api:3.0.0'
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.8.15'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'com.nickuc.chat:nchat-api:5.6'
    compileOnly fileTree('libs')
    implementation fileTree(dir: 'libs', include: ['inventory-api-2.0.4-FINAL.jar'])

    implementation 'io.github.juliarn:npc-lib-api:3.0.0-beta11'
    implementation 'io.github.juliarn:npc-lib-bukkit:3.0.0-beta11'
    implementation 'io.github.juliarn:npc-lib-common:3.0.0-beta11'

    implementation 'com.github.HenryFabio.configuration-injector:bukkit:1.0.2'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'

    implementation 'com.github.SaiintBrisson.command-framework:bukkit:1.3.1'
    implementation 'de.tr7zw:item-nbt-api:2.14.1'

    implementation 'com.github.ben-manes.caffeine:caffeine:2.9.3' // 3.0.0 ou superior Ã© para Java 11+
    implementation 'org.apache.commons:commons-lang3:3.17.0'

    def lombok = 'org.projectlombok:lombok:1.18.36'
    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.economy.NextEconomy'
    authors = ['NextPlugins']
    website = 'https://nextplugins.com.br'
    version = "${project.version}"
    depend = ['Vault']
    apiVersion = '1.13'
    load = 'STARTUP'
    softDepend = [
            'DecentHolograms',
            'ProtocolLib',
            'NextTestServer',
            'CMI',
            'SkinsRestorer',
            'HolographicDisplays',
            'PlaceholderAPI',
            'Legendchat',
            'UltimateChat',
            'nChat',
            'DiscordSRV'
    ]
}

tasks.named('shadowJar', ShadowJar) {
    archiveFileName.set("${project.name}-${project.version}.jar")

    relocate 'com.github.juliarn.npclib', 'com.nextplugins.economy.libs.npclib'
    relocate 'com.yuhtin.updatechecker', 'com.nextplugins.economy.libs.updatechecker'

    relocate 'com.henryfabio.minecraft.configinjector', 'com.nextplugins.economy.libs.configinjector'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.economy.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.economy.libs.hikari'

    relocate 'com.github.benmanes.caffeine', 'com.nextplugins.economy.libs.caffeine'

    relocate 'me.saiintbrisson.minecraft.command', 'com.nextplugins.economy.libs.command.common'
    relocate 'me.saiintbrisson.bukkit.command', 'com.nextplugins.economy.libs.command.bukkit'

    relocate 'de.tr7zw.changeme.nbtapi', 'com.nextplugins.economy.libs.nbtapi'
    relocate 'de.tr7zw.annotations', 'com.nextplugins.economy.libs.annotations'

    relocate 'com.google.errorprone.annotations', 'com.nextplugins.economy.libs.annotations'
}

compileJava {
    options.encoding = 'UTF-8'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}